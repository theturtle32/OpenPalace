<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/halo" minWidth="800" minHeight="450"
	skinClass="FlashPalaceApplicationSkin"
	applicationComplete="handleApplicationComplete()"
	creationComplete="handleCreationComplete()" xmlns:code="http://code.google.com/p/flexlib/" xmlns:view="net.codecomposer.palace.view.*" xmlns:local="*">
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
 
	<fx:Script>
		<![CDATA[
			import mx.utils.URLUtil;
			import mx.events.BrowserChangeEvent;
			import mx.messaging.config.ServerConfig;
			import mx.managers.BrowserManager;
			import mx.managers.IBrowserManager;
			import flexlib.mdi.events.MDIWindowEvent;
			import spark.components.ButtonBarButton;
			import net.codecomposer.palace.view.UserListGrid;
			import mx.events.PropertyChangeEvent;
			import mx.binding.utils.ChangeWatcher;
			import net.codecomposer.palace.rpc.PalaceClient;
			[Bindable]
			public var palace:PalaceClient = PalaceClient.getInstance();

			private var connectWindow:ConnectWindow;
			private var roomListWindow:RoomListWindow;
			private var userListWindow:UserListWindow;
			private var logWindow:LogWindow;
			private var preferencesWindow:PreferencesWindow;
			
			private var preferences:Preferences = Preferences.getInstance();
			
			private var browserManager:IBrowserManager;

			private function handleApplicationComplete():void {
				stage.addEventListener(KeyboardEvent.KEY_DOWN, handleStageKeyDown);
			}
			
			private function handleStageKeyDown(event:KeyboardEvent):void {
				if (palace.connected) {
					if (event.keyCode == Keyboard.NUMPAD_ADD) {
						palace.setColor(palace.currentUser.color+1);
					}
					else if (event.keyCode == Keyboard.NUMPAD_SUBTRACT) {
						palace.setColor(palace.currentUser.color-1);
					}
				}
			}

			private function handleCreationComplete():void {
				browserManager = BrowserManager.getInstance();
				browserManager.init("", "No Connection - OpenPalace");
				browserManager.addEventListener(BrowserChangeEvent.BROWSER_URL_CHANGE, handleBrowserURLChange);
				handleBrowserURLChange();
				
				ChangeWatcher.watch(palace, 'connected', handleConnectedChange);
				ChangeWatcher.watch(palace, ['currentRoom','name'], handleRoomChange);
				showConnectWindow();
			}
			
			private function updateTitle(serverName:String, roomName:String):void {
				browserManager.setTitle(roomName + " - " + serverName + " - OpenPalace");
			}
			
			private function handleConnectedChange(event:PropertyChangeEvent):void {
				if (palace.connected) {
					if (connectWindow) {
						mdic.windowManager.remove(connectWindow);
						connectWindow = null;
					}
				}
				else {
					browserManager.setTitle("No Connection - OpenPalace");
				}
			}
			
			private function handleRoomChange(event:PropertyChangeEvent):void {
				if (palace.connected) {
					updateTitle(palace.serverName, palace.currentRoom.name);
				}
			}
			
			private function handleBrowserURLChange(event:BrowserChangeEvent=null):void {
				trace("URL Changed: " + browserManager.fragment);
				var parameters:Object = URLUtil.stringToObject(browserManager.fragment);
				if (parameters.palace != null) {
					var url:String = String(parameters.palace).toLowerCase();
					var match:Array = url.match(/^palace:\/\/(.*)$/);
					if (match && match.length > 0) {
						url = match[1];
					}
					
					var parts:Array = url.split(':');
					var hostName:String = parts[0];
					var port:Number = 9998;
					if ( parts[1] && String(parts[1]).length > 0 ) {
						port = uint(parts[1]);
					} 
					trace("palace = " + hostName + " port " + port);
					
					if (hostName && hostName.length > 0) {
						palace.connect(preferences.userName, hostName, port);
					}
				}
			}
			
			private function disconnect():void {
				palace.disconnect();
			}
			
			private function showConnectWindow():void {
				if (!connectWindow) {
					connectWindow = new ConnectWindow();
				}
				mdic.windowManager.add(connectWindow);
				mdic.windowManager.bringToFront(connectWindow);
			}
			
			private function showLogWindow():void {
				logWindow = new LogWindow();
				logWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					logButton.selected = false;
				});
				mdic.windowManager.add(logWindow);
			}
			
			private function hideLogWindow():void {
				mdic.windowManager.remove(logWindow);
				logWindow = null;
			}
			
			private function handleLogClick(event:MouseEvent):void {
				if (ButtonBarButton(event.target).selected) {
					showLogWindow();
				}
				else {
					hideLogWindow();
				}
			}
			
			private function showUserList():void {
				palace.requestUserList();
				userListWindow = new UserListWindow();
				userListWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					usersButton.selected = false;
				});
				mdic.windowManager.add(userListWindow);
			}
			
			private function hideUserList():void {
				mdic.windowManager.remove(userListWindow);
				userListWindow = null;
			}
			
			private function handleUsersClick(event:MouseEvent):void {
				if (ButtonBarButton(event.target).selected) {
					showUserList();
				}
				else {
					hideUserList();
				}
			}
			
			private function showPreferences():void {
				preferencesWindow = new PreferencesWindow();
				preferencesWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					preferencesButton.selected = false;
				});
				mdic.windowManager.add(preferencesWindow);
			}
			
			private function hidePreferences():void {
				mdic.windowManager.remove(preferencesWindow);
				preferencesWindow = null;
			}
			
			private function handlePreferencesClick(event:MouseEvent):void {
				if (ButtonBarButton(event.target).selected) {
					showPreferences();
				}
				else {
					hidePreferences();
				}
			}
						
			private function showRoomList():void {
				palace.requestRoomList();
				roomListWindow = new RoomListWindow();
				roomListWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					roomsButton.selected = false;
				});
				mdic.windowManager.add(roomListWindow);
			}
			
			private function hideRoomList():void {
				mdic.windowManager.remove(roomListWindow);
				roomListWindow = null;
			}
			
			private function handleRoomsClick(event:MouseEvent):void {
				if (ButtonBarButton(event.target).selected) {
					showRoomList();
				}
				else {
					hideRoomList();
				}
			}
			

			protected function handleZoomChange(event:Event):void
			{
				chatWindow.scaleX = chatWindow.scaleY = zoomSlider.value;
			}

		]]>
	</fx:Script>

	<s:HGroup top="5" left="5" gap="1" verticalAlign="middle">
		<!-- <s:ButtonBarButton label="Palaces" /> -->
		<s:ButtonBarButton id="roomsButton" label="Rooms" click="handleRoomsClick(event)" />
		<s:ButtonBarButton id="usersButton" label="Users" click="handleUsersClick(event)" />
		<s:ButtonBarButton id="logButton" label="Log" click="handleLogClick(event)" />
		<s:ButtonBarButton id="preferencesButton" label="Preferences" click="handlePreferencesClick(event)" />
		<mx:Spacer width="15" />
		<s:SimpleText text="Zoom:" />
		<s:HSlider id="zoomSlider" width="75" minimum="1" maximum="1.5" valueInterval="0.01" liveDragging="true" change="handleZoomChange(event)" />
	</s:HGroup>
			
	
	<s:HGroup top="5" right="5" verticalAlign="middle">
		<s:SimpleText text="{palace.currentRoom.users.length}/{palace.population}" />
		<s:Button label="Disconnect" click="disconnect()" enabled="{palace.connected}" />
		<s:Button label="Connect" click="showConnectWindow()" />
	</s:HGroup>

	<local:ChatWindow id="chatWindow" palace="{palace}" top="40" left="10" />	
	
	<code:MDICanvas id="mdic" width="100%" height="100%" >
	</code:MDICanvas>

</s:Application>
