<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/halo" minWidth="800" minHeight="450"
	skinClass="FlashPalaceApplicationSkin"
	applicationComplete="handleApplicationComplete()"
	creationComplete="handleCreationComplete()" xmlns:code="http://code.google.com/p/flexlib/" xmlns:view="net.codecomposer.palace.view.*" xmlns:local="*">
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
 
	<fx:Script>
		<![CDATA[
			import com.pixelbreaker.ui.osx.MacMouseWheel;
			
			import flexlib.mdi.events.MDIWindowEvent;
			
			import mx.binding.utils.ChangeWatcher;
			import mx.controls.Alert;
			import mx.events.BrowserChangeEvent;
			import mx.events.CloseEvent;
			import mx.events.PropertyChangeEvent;
			import mx.managers.BrowserManager;
			import mx.managers.IBrowserManager;
			import mx.utils.URLUtil;
			
			import net.codecomposer.palace.event.PalaceEvent;
			import net.codecomposer.palace.rpc.PalaceClient;
			[Bindable]
			public var palace:PalaceClient = PalaceClient.getInstance();

			private var directoryWindow:DirectoryWindow;
			private var connectWindow:ConnectWindow;
			private var roomListWindow:RoomListWindow;
			private var userListWindow:UserListWindow;
			private var logWindow:LogWindow;
			private var preferencesWindow:PreferencesWindow;
			private var propsWindow:PropsWindow;
			private var newPropWindow:NewPropWindow;
			private var debugWindow:DebugWindow;
			
			private var preferences:Preferences = Preferences.getInstance();
			
			private var browserManager:IBrowserManager;

			private function handleApplicationComplete():void {
				stage.addEventListener(KeyboardEvent.KEY_DOWN, handleStageKeyDown);
				MacMouseWheel.setup(stage);
			}
			
			private function handleStageKeyDown(event:KeyboardEvent):void {
				if (palace.connected) {
					var newColor:int;
					if (event.keyCode == Keyboard.NUMPAD_ADD) {
						newColor = palace.currentUser.color + 1;
						if (newColor > 15) {
							newColor = 0;
						}
						palace.setColor(newColor);
					}
					else if (event.keyCode == Keyboard.NUMPAD_SUBTRACT) {
						newColor = palace.currentUser.color - 1;
						if (newColor < 0) { newColor = 15; }
						palace.setColor(newColor);
					}
				}
			}

			private function handleCreationComplete():void {
				browserManager = BrowserManager.getInstance();
				browserManager.init("", "No Connection - OpenPalace");
				browserManager.addEventListener(BrowserChangeEvent.BROWSER_URL_CHANGE, handleBrowserURLChange);
				handleBrowserURLChange();
				
				ChangeWatcher.watch(palace, ['currentRoom','name'], handleRoomChange);
				showDirectoryWindow();
				
				palace.addEventListener(PalaceEvent.CONNECT_COMPLETE, handleConnectComplete);
				palace.addEventListener(PalaceEvent.CONNECT_FAILED, handleConnectFailed);
				palace.addEventListener(PalaceEvent.CONNECT_START, handleConnectStart);
				palace.addEventListener(PalaceEvent.DISCONNECTED, handleDisconnected);
				palace.addEventListener(PalaceEvent.GOTO_URL, handleGotoURL);
			}
			
			private function handleGotoURL(event:PalaceEvent):void {
				var url:String = event.url;
				Alert.show("Do you want to visit this url in your browser?\n\n" + event.url,
						   "External Link",
						   Alert.YES | Alert.NO,
						   null,
						   function(event:CloseEvent):void {
							  if (event.detail == Alert.YES) {
								  var req:URLRequest = new URLRequest(url);
								  navigateToURL(req, "_BLANK");
							  }
						   });
			}
			
			private function handleConnectComplete(event:PalaceEvent):void {
				hideConnectWindow();
				hideDirectoryWindow();
				chatWindow.currentState = "connected";
			}
			
			private function handleConnectFailed(event:PalaceEvent):void {
				showConnectWindow();
				Alert.show(event.text, "Error");
				chatWindow.currentState = "disconnected"
			}
			
			private function handleConnectStart(event:PalaceEvent):void {
				hideConnectWindow();
				hideDirectoryWindow();
				chatWindow.currentState = "connecting";
			}
			
			private function handleDisconnected(event:PalaceEvent):void {
				browserManager.setTitle("No Connection - OpenPalace");
			}
			
			private function updateTitle(serverName:String, roomName:String):void {
				browserManager.setTitle(roomName + " - " + serverName + " - OpenPalace");
			}
			
			private function handleRoomChange(event:PropertyChangeEvent):void {
				if (palace.connected) {
					updateTitle(palace.serverName, palace.currentRoom.name);
				}
			}
			
			private function handleBrowserURLChange(event:BrowserChangeEvent=null):void {
				trace("URL Changed: " + browserManager.fragment);
				var parameters:Object = URLUtil.stringToObject(browserManager.fragment);
				if (parameters.palace != null) {
					var url:String = String(parameters.palace).toLowerCase();
					var match:Array = url.match(/^(palace:\/\/){0,1}(.*?):?([0-9]*)\/?([0-9]*)$/);
					var port:uint = 9998;
					var initialRoom:uint = 0;
					var hostName:String;
					if (match && match.length > 0) {
						hostName = match[2];
						if (match[3]) {
							port = parseInt(match[3]);
						}
						if (match[4]) {
							initialRoom = parseInt(match[4]);
						}
					}
					
					if (hostName && hostName.length > 0) {
						palace.connect(preferences.userName, hostName, port, initialRoom);
					}
				}
			}
			
			private function disconnect():void {
				palace.disconnect();
			}
			
			private function showConnectWindow():void {
				if (!connectWindow) {
					connectWindow = new ConnectWindow();
				}
				mdic.windowManager.add(connectWindow);
				mdic.windowManager.bringToFront(connectWindow);
			}
			
			private function hideConnectWindow():void {
				if (connectWindow) {
					try {
						mdic.windowManager.remove(connectWindow);
					}
					catch (e:Error) {
						// do nothing
					}
					connectWindow = null;
				}
			}
			
			private function showLogWindow():void {
				logWindow = new LogWindow();
				logWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					logButton.selected = false;
				});
				mdic.windowManager.add(logWindow);
			}
			
			private function hideLogWindow():void {
				mdic.windowManager.remove(logWindow);
				logWindow = null;
			}
			
			private function showPropsWindow():void {
				propsWindow = new PropsWindow();
				propsWindow.palace = palace;
				propsWindow.addEventListener(PropEvent.NEW_PROP, handleNewProp);
				propsWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					propsButton.selected = false;
				});
				mdic.windowManager.add(propsWindow);
			}
			
			private function hidePropsWindow():void {
				mdic.windowManager.remove(propsWindow);
				propsWindow = null;
			}
			
			private function handleNewProp(event:PropEvent):void {
				trace("New Prop: " + event.fileReference.name);
				newPropWindow = new NewPropWindow();
				newPropWindow.addEventListener(MDIWindowEvent.CLOSE, handleNewPropWindowClose);
				newPropWindow.addEventListener(PropEvent.SAVE_PROP, handleSaveProp);
				mdic.windowManager.add(newPropWindow);
				newPropWindow.fileReference = event.fileReference;
			}
			
			private function handleSaveProp(event:PropEvent):void {
				trace("Prop Name: " + event.newPropDefinition.name);
				if (propsWindow) {
					propsWindow.addNewProp(event.newPropDefinition);
				}
			}
			
			private function handleNewPropWindowClose(event:MDIWindowEvent):void {
				trace("New Prop Window Close");
			}
			
			private function handlePropsClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showPropsWindow();
				}
				else {
					hidePropsWindow();
				}
			}
			
			private function handleLogClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showLogWindow();
				}
				else {
					hideLogWindow();
				}
			}
			
			private function showUserList():void {
				palace.requestUserList();
				userListWindow = new UserListWindow();
				userListWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					usersButton.selected = false;
				});
				mdic.windowManager.add(userListWindow);
			}
			
			private function hideUserList():void {
				mdic.windowManager.remove(userListWindow);
				userListWindow = null;
			}
			
			private function handleUsersClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showUserList();
				}
				else {
					hideUserList();
				}
			}
			
			private function showPreferences():void {
				preferencesWindow = new PreferencesWindow();
				preferencesWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					preferencesButton.selected = false;
				});
				mdic.windowManager.add(preferencesWindow);
			}
			
			private function hidePreferences():void {
				mdic.windowManager.remove(preferencesWindow);
				preferencesWindow = null;
			}
			
			private function handlePreferencesClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showPreferences();
				}
				else {
					hidePreferences();
				}
			}
			
			private function showDebugWindow():void {
				debugWindow = new DebugWindow();
				debugWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					debugButton.selected = false;
				});
				mdic.windowManager.add(debugWindow);
			}
			
			private function hideDebugWindow():void {
				mdic.windowManager.remove(debugWindow);
				debugWindow = null;
				palace.palaceController.scriptManager.resume();
			}
			
			private function handleDebugClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showDebugWindow();
				}
				else {
					hideDebugWindow();
				}
			}
			
			private function showDirectoryWindow():void {
				directoryWindow = new DirectoryWindow();
				directoryWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					directoryButton.selected = false;
				});
				mdic.windowManager.add(directoryWindow);
			}
			
			private function hideDirectoryWindow():void {
				if (directoryWindow) {
					try {
						mdic.windowManager.remove(directoryWindow);
					}
					catch(e:Error) {
						// do nothing
					}
					directoryWindow = null;
					directoryButton.selected = false;
				}
			}
			
			private function handleDirectoryClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showDirectoryWindow();
				}
				else {
					hideDirectoryWindow();
				}
			}
						
			private function showRoomList():void {
				palace.requestRoomList();
				roomListWindow = new RoomListWindow();
				roomListWindow.addEventListener(MDIWindowEvent.CLOSE, function(event:Event):void {
					roomsButton.selected = false;
				});
				mdic.windowManager.add(roomListWindow);
			}
			
			private function hideRoomList():void {
				mdic.windowManager.remove(roomListWindow);
				roomListWindow = null;
			}
			
			private function handleRoomsClick(event:MouseEvent):void {
				if (ToggleButton(event.target).selected) {
					showRoomList();
				}
				else {
					hideRoomList();
				}
			}
			

			protected function handleZoomChange(event:Event):void
			{
				chatWindow.roomView.scaleX = chatWindow.roomView.scaleY = zoomSlider.value;
			}

		]]>
	</fx:Script>

	<s:HGroup top="5" left="5" gap="1" verticalAlign="middle">
		<s:ToggleButton id="roomsButton" label="Rooms" click="handleRoomsClick(event)" />
		<s:ToggleButton id="usersButton" label="Users" click="handleUsersClick(event)" />
		<s:ToggleButton id="logButton" label="Log" click="handleLogClick(event)" />
		<s:ToggleButton id="propsButton" label="Props" click="handlePropsClick(event)" />
		<s:ToggleButton id="preferencesButton" label="Preferences" click="handlePreferencesClick(event)" />
		<s:ToggleButton id="debugButton" label="Debug" click="handleDebugClick(event)" visible="false" includeInLayout="false" />
		<mx:Spacer width="15" />
		<s:Label text="Zoom:" />
		<s:HSlider id="zoomSlider" width="75" value="1.0" minimum="0.5" maximum="1.5" snapInterval="0.01" liveDragging="true" change="handleZoomChange(event)" />
	</s:HGroup>
			
	
	<s:HGroup top="5" right="5" verticalAlign="middle">
		<s:Label text="{palace.currentRoom.users.length}/{palace.population}" />
		<s:Button label="Disconnect" click="disconnect()" enabled="{palace.connected}" />
		<s:ToggleButton selected="true" label="Directory" id="directoryButton" click="handleDirectoryClick(event)" />
		<s:Button label="Connect" click="showConnectWindow()" />
	</s:HGroup>

	<local:ChatWindow id="chatWindow" palace="{palace}" top="40" left="10" />	
	
	<code:MDICanvas id="mdic" width="100%" height="100%" >
	</code:MDICanvas>

</s:Application>
