<?xml version="1.0" encoding="utf-8"?>

<!--
This file is part of OpenPalace.

OpenPalace is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

OpenPalace is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with OpenPalace.  If not, see <http://www.gnu.org/licenses/>.
-->

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	width="{backgroundImage.width}" height="{backgroundImage.height}"
	minWidth="512" minHeight="384"
	xmlns:view="net.codecomposer.palace.view.*"
	backgroundColor="#000000"
	creationComplete="handleCreationComplete()">
	<mx:Script>
		<![CDATA[
			import flash.utils.setTimeout;
			import mx.core.UIComponent;
			import mx.binding.utils.BindingUtils;
			import net.codecomposer.palace.event.PalaceRoomEvent;
			import net.codecomposer.palace.event.AvatarSelectEvent;
			import net.codecomposer.palace.event.ChatEvent;
			import net.codecomposer.palace.model.PalaceCurrentRoom;
			import net.codecomposer.palace.rpc.PalaceClient;
			import mx.events.PropertyChangeEvent;
			import net.codecomposer.palace.model.PalaceUser;
			import mx.binding.utils.ChangeWatcher;
			import net.codecomposer.palace.model.PalaceCurrentRoom;
			[Bindable]
			public var room:PalaceCurrentRoom;
			
			[Bindable]
			public var mediaServer:String;
			
			private var avatarsByUser:Dictionary = new Dictionary();
			private var nameTagsByUser:Dictionary = new Dictionary();
			
			private var baseBackgroundFile:String;
			private var forcingJpeg:Boolean = false;
			
			private var isAir:Boolean;
			
			private function handleCreationComplete():void {
				this.addEventListener(MouseEvent.CLICK, handleClick);
				room.addEventListener(ChatEvent.CHAT, handleChat);
				room.addEventListener(PalaceRoomEvent.ROOM_CLEARED, handleRoomCleared);
				room.addEventListener(PalaceRoomEvent.USER_ENTERED, handleUserEntered);
				room.addEventListener(PalaceRoomEvent.USER_LEFT, handleUserLeft);
				ChangeWatcher.watch(room, 'backgroundFile', handleBGChange);
			}
			
			private function getHitArea():UIComponent {
				var hitArea:UIComponent = new UIComponent();
				hitArea.visible = false;
				hitArea.mouseEnabled = false;
				hitArea.graphics.clear();
				hitArea.graphics.beginFill(0x000000, 1.0);
				hitArea.graphics.drawRect(44,44,44,44);
				hitArea.height = hitArea.width = 44;
				hitArea.graphics.endFill();
				return hitArea;
			}
			
			private function handleClick(event:MouseEvent):void {
				var x:int = this.contentMouseX;
				var y:int = this.contentMouseY;
				trace("Movement requested x:" + x + " y:" + y);
				var socket:PalaceClient = PalaceClient.getInstance();
				socket.move(x, y);
			}
			
			private function handleAvatarClick(event:MouseEvent):void {
				if (event.currentTarget is Avatar) {
					var user:PalaceUser = Avatar(event.currentTarget).user;
					trace("Clicked avatar " + user.id + " - self is " + room.selfUserId);
					if (user.id == room.selfUserId) {
						room.selectedUser = null;
					} 
					else if (room.selectedUser != user) {
						room.selectedUser = user;
					}
					else if (room.selectedUser == user) {
						room.selectedUser = null;
					}
					updateAvatarTransparencies();
					event.stopPropagation();
				}
			}
			
			private function updateAvatarTransparencies():void {
				//trace("Updating avatar transparencies");
				for each (var avatar:Object in avatarsByUser) {
					var av:Avatar = Avatar(avatar);
					if (room.selectedUser == null || av.user.id == room.selfUserId || av.user == room.selectedUser) {
						//trace("User " + av.user.id + " is self or selected user or no user is selected");
						av.alpha = 1;
					}
					else {
						//trace("User " + av.user.id + " is not the selected user (" + room.selectedUser.id + ")");
						av.alpha = 0.6;
					}
				}
			}
			
			private function handleChat(event:ChatEvent):void {
				var chatBubble:ChatBubble = new ChatBubble();
				chatBubble.setStyle('skinClass', Class(net.codecomposer.palace.view.ChatBubbleSkin));
				chatBubble.text = event.chatText;
				chatBubble.x = event.user.x;
				chatBubble.y = event.user.y;
				textCanvas.addChild(chatBubble)
				setTimeout(function():void {
					textCanvas.removeChild(chatBubble);
				}, 1400 + (event.chatText.length * 45));
			}
			
			private function handleUserEntered(event:PalaceRoomEvent):void {
				var avatar:Avatar = new Avatar();
				avatar.user = event.user;
				avatar.alpha = 1;
				avatar.addEventListener(MouseEvent.CLICK, handleAvatarClick);
				avatarsCanvas.addChild(avatar);
				var hitArea:UIComponent = getHitArea();
				avatar.addChild(hitArea);
				avatar.hitArea = hitArea;
				avatar.validateDisplayList();
				avatarsByUser[event.user] = avatar;
				updateAvatarTransparencies();
				
//				var nameTag:NameTag = new NameTag();
//				nameTag.user = event.user;
//				nameTagsByUser[event.user] = nameTag;
//				nameTagsCanvas.addChild(nameTag);
			}
			
			private function handleUserLeft(event:PalaceRoomEvent):void {
				var avatar:Avatar = avatarsByUser[event.user];
				if (avatar != null) {
					avatar.removeEventListener(MouseEvent.CLICK, handleAvatarClick);
					avatarsCanvas.removeChild(avatar);
					avatarsByUser[event.user] = null;
					delete avatarsByUser[event.user];
				}
								
//				var nameTag:NameTag = nameTagsByUser[event.user];
//				if (nameTag != null) {
//					nameTagsCanvas.removeChild(nameTag);
//					nameTagsByUser[event.user] = null;
//					delete nameTagsByUser[event.user];
//				}
			}
			
			private function handleRoomCleared(event:PalaceRoomEvent):void {
				for each (var avatar:Object in avatarsByUser) {
					Avatar(avatar).removeEventListener(MouseEvent.CLICK, handleAvatarClick);
				}
				avatarsCanvas.removeAllChildren();
				avatarsByUser = new Dictionary();
				
//				for each (var nameTag:Object in nameTagsByUser) {
//					nameTagsCanvas.removeAllChildren();
//					nameTagsByUser = new Dictionary();
//				}
			}
			
			private function handleBGChange(event:PropertyChangeEvent):void {
				if (room.backgroundFile == null) {
					return;
				}
				var match:Array = room.backgroundFile.match(/^(.*)\.gif$/i);
				if (match != null && Security.sandboxType == Security.APPLICATION) {
					forcingJpeg = true;
					baseBackgroundFile = match[1];
					trace("Trying to find jpeg version of background."); 
					backgroundImage.source = mediaServer + baseBackgroundFile + ".jpg";
				}
				else {
					backgroundImage.source = mediaServer + room.backgroundFile;
				}
			}
			
			private function handleBGHTTP(event:HTTPStatusEvent):void {
				if (forcingJpeg && event.status == 404) {
					trace("Unable to load " + backgroundImage.source + " - Reverting to gif.");
					forcingJpeg = false;
					setTimeout(function():void {
						backgroundImage.source = mediaServer + room.backgroundFile;
						trace("Loading: " + backgroundImage.source);
					}, 50);
				}
			}
			
		]]>
	</mx:Script>
	<mx:Image top="0" left="0" id="backgroundImage" minHeight="384" minWidth="512"
		scaleContent="false" httpStatus="handleBGHTTP(event)"
		loaderContext="{PalaceClient.loaderContext}" 
		visible="{room.backgroundFile != null}" />
	<view:PalaceHotspotView id="hotSpotCanvas" room="{room}" mediaServer="{mediaServer}" hotSpots="{room.hotSpots}" top="0" right="0" bottom="0" left="0" horizontalScrollPolicy="off" verticalScrollPolicy="off" />
	<mx:Canvas id="loosePropsCanvas" horizontalScrollPolicy="off" verticalScrollPolicy="off" top="0" right="0" bottom="0" left="0">
		<mx:Repeater id="loosePropsRepeater" dataProvider="{room.looseProps}">
			<mx:Image source="{loosePropsRepeater.currentItem.prop.bitmap}"
				alpha="{loosePropsRepeater.currentItem.prop.ghost ? 0.5 : 1.0}"
				x="{loosePropsRepeater.currentItem.x}"
				y="{loosePropsRepeater.currentItem.y}" />
		</mx:Repeater>
	</mx:Canvas>
	
	<mx:Canvas id="avatarsCanvas" top="0" right="0" bottom="0" left="0" horizontalScrollPolicy="off" verticalScrollPolicy="off" />
<!--	<mx:Canvas id="nameTagsCanvas" top="0" right="0" bottom="0" left="0" horizontalScrollPolicy="off" verticalScrollPolicy="off"  /> -->
	<mx:Canvas id="textCanvas" top="0" right="0" bottom="0" left="0" horizontalScrollPolicy="off" verticalScrollPolicy="off" />
</mx:Canvas>
